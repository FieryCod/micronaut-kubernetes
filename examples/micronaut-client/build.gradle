import org.gradle.nativeplatform.platform.OperatingSystem
import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

plugins {
    id "com.github.johnrengelman.shadow" version "5.2.0"
    id "application"
    id "com.google.cloud.tools.jib" version "1.7.0"
    id "java"
    id "net.ltgt.apt-eclipse" version "0.21"
    id "net.ltgt.apt-idea" version "0.21"
}

repositories {
    jcenter()
}

configurations {
    // for dependencies that are needed for development only
    developmentOnly
}

dependencies {
    annotationProcessor("io.micronaut:micronaut-inject-java")
    annotationProcessor("io.micronaut:micronaut-validation")
    implementation("io.micronaut:micronaut-inject")
    implementation("io.micronaut:micronaut-validation")
    implementation("io.micronaut:micronaut-runtime")
    implementation("javax.annotation:javax.annotation-api")
    implementation("io.micronaut:micronaut-http-server-netty")
    implementation("io.micronaut:micronaut-http-client")

    implementation 'io.micronaut:micronaut-management'
    implementation project(':kubernetes-discovery-client')

    runtimeOnly("ch.qos.logback:logback-classic:1.2.3")
    testImplementation("io.micronaut:micronaut-inject-groovy")
    testImplementation("org.spockframework:spock-core") {
        exclude group: "org.codehaus.groovy", module: "groovy-all"
    }
    testImplementation("io.micronaut.test:micronaut-test-spock")

    testCompile project(":test-utils")
}

shadowJar {
    mergeServiceFiles()
}

test.classpath += configurations.developmentOnly

mainClassName = "micronaut.client.Application"

OperatingSystem os = DefaultNativePlatform.currentOperatingSystem

jib {
    from {
//        image = 'gcr.io/distroless/java:debug'
        image = 'alvarosanchez/alpine-with-curl'
    }
    to {
        image = System.getenv('CI') ? 'registry.hub.docker.com/alvarosanchez/example-client' : 'micronaut-kubernetes-example-client'
        if (os.isMacOsX()) {
            credHelper = 'osxkeychain'
        }
    }
}
tasks.withType(JavaCompile){
    options.encoding = "UTF-8"
    options.compilerArgs.add('-parameters')
}

tasks.withType(JavaExec) {
    classpath += configurations.developmentOnly
    jvmArgs('-XX:TieredStopAtLevel=1', '-Dcom.sun.management.jmxremote')
}
